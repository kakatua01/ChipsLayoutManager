apply plugin: 'com.android.application'

android {
    signingConfigs {
        debug {
            keyAlias 'debugKey'
            keyPassword 'tester'
            storeFile file('../debug.jks')
            storePassword 'tester'
        }
    }
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion
    testBuildType "development"
    defaultConfig {
        applicationId "com.beloo.chipslayoutmanager.sample"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 1
        versionName rootProject.ext.libraryVersion

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        development {
            buildConfigField("boolean", "isReportCrashes", "false")
            debuggable true
            signingConfig signingConfigs.debug
        }

        debug {
            buildConfigField("boolean", "isReportCrashes", "true")
        }

        release {
            buildConfigField("boolean", "isReportCrashes", "true")
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    //modules
    compile project(':ChipsLayoutManager')
    //support libraries
    compile "com.android.support:appcompat-v7:$rootProject.ext.supportVersion"
    compile "com.android.support:design:$rootProject.ext.supportVersion"
    compile "com.android.support:recyclerview-v7:$rootProject.ext.supportVersion"
    compile "com.android.support:cardview-v7:$rootProject.ext.supportVersion"
    compile "com.android.support:support-v4:$rootProject.ext.supportVersion"
    //3rd party libraries
    compile('com.mikepenz:materialdrawer:5.8.0@aar') {
        transitive = true
    }
    compile 'com.github.bumptech.glide:glide:3.7.0'
    //butterKnife
    compile 'com.jakewharton:butterknife:8.5.1'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.5.1'
    //crash tracking tool
    compile 'com.google.firebase:firebase-crash:11.2.0'
    testCompile 'junit:junit:4.12'
    //android tests
    androidTestCompile "com.android.support:support-annotations:$rootProject.ext.supportVersion"
    androidTestCompile "com.android.support.test.espresso:espresso-core:$rootProject.ext.espresso"
    androidTestCompile 'com.android.support.test:runner:1.0.0'
    androidTestCompile 'org.mockito:mockito-core:2.9.0'
    androidTestCompile 'com.linkedin.dexmaker:dexmaker-mockito:2.2.0'
    androidTestCompile("com.android.support.test.espresso:espresso-contrib:3.0.0") {
        //this library has conflicts with app dependencies
        exclude group: 'com.android.support', module: 'appcompat'
        exclude group: 'com.android.support', module: 'support-v4'
        exclude group: 'com.android.support', module: 'design'
        exclude module: 'recyclerview-v7'
    }
}


apply plugin: 'com.google.gms.google-services'